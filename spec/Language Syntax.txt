# Language Syntax

program          = (statement | comment)*
statement        = ifstmt | loop | print | input | block | return | break | continue | exprstmt
ifstmt           = '??' expression statement ('::' statement)?
expression       = assignexpr
assignexpr       = ifexpr (('=' | ":=") assignexpr)?
ifexpr           = logic ('?' ifexpr ':' ifexpr)?
logic            = equality (("||" | "&&") logic)?
equality         = relation (("==" | "!=") equality)?
relation         = basic ((">" | "<" | ">=" | "<=") relation)?
basic            = factor (("+" | "-") basic)?
factor           = unary (("*" | "/" | "%") factor)?
unary            = ("!" | "+" | "-" | "#") unary | postary
postary          = postary ((arguments) | (indexing)) | primary
arguments        = '(' (expression (',' expression)*)? ')'
indexing         = '[' (expression | (arr-range)) (',' (expression | arr-range))? ']'
arr-range        = (expression)? ':' (expression)?
primary          = ident | float | string | array | prototype | grouping | lambda | builder
ident            = (alphabet | '_') (alphabet | '_' | digit)*
alphabet         = ('a'->'z' | 'A->Z')
digit            = '0'->'9'
float            = number ('.' number)?
number           = (digit)+
array            = '[' (expression (',' expression)*)? ']'
range            = expression range-op ident range-op expression
range-op         = '>' | '>=' | '<' | '<='
string           = '"' (writable)* '"'
writable         = ' ' -> '~'
grouping         = '(' expression ')'
lambda           = "(" ident ("," ident)* ")" block
builder          = '[' (range)? ';' (expression)? ';' expression ']'
prototype        = '{' (ident ('=' | ':=') expression)* '}'
loop             = ">>" expression statement
block            = '{' (statement)* '}'
print            = '@' expression
input            = '$'
exprstmt         = expression
return           = "->" (expression)?
break            = "**"
continue         = "<<"
comment          = "//" writable*